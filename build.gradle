buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.github.johnrengelman.shadow" version "4.0.4"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mod_version}"
group = "${mod_group}"
archivesBaseName = "${mod_name}-${mc_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {

    mappings channel: 'snapshot', version: '20200514-1.16'


    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            mods {
                iammusicplayer {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            mods {
                iammusicplayer {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            args '--mod', "iammusicplayer", '--all', '--output', file('src/generated/resources/')

            mods {
                iammusicplayer {
                    source sourceSets.main
                }
            }
        }
    }
}
repositories {
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "Jei"
        url = "https://modmaven.k-4u.nl"
    }
    maven { url "https://maven.latmod.com/" }
}
configurations {
    shadow
    compile.extendsFrom(shadow)
}
dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    compile fg.deobf(files("libs/OtyacraftEngine-${oe_version}-dev.jar"))
    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")
    compile fg.deobf("com.feed_the_beast.mods:ftb-gui-library:${ftb_gui_library_version}")
    shadow 'com.mpatric:mp3agic:0.9.1'
    shadow 'ws.schild:jave-all-deps:3.0.1'
}
shadowJar.configurations = [project.configurations.shadow]
shadowJar {
    project.configurations.shadow.setTransitive(true);
    classifier ''
    relocate 'com.mpatric', 'red.felnull.imp.libs.com.mpatric'
    relocate 'ws.schild.jave', 'red.felnull.imp.libs.ws.schild.jave'
    relocate 'org.slf4j', 'red.felnull.imp.libs.org.slf4j'
}
afterEvaluate {
    reobf {
        shadowJar { mappings = tasks.reobfJar.mappings }
    }
}
ext.allmanifest = {
    attributes([
            "Specification-Title"     : "${mod_id}",
            "Specification-Vendor"    : "MORIMORI0317",
            "Specification-Version"   : "1",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${version}",
            "Implementation-Vendor"   : "TeamFelNull",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
}
jar {
    manifest allmanifest
}
task devJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.allJava
    classifier = 'dev'
    manifest allmanifest
}
artifacts {
    archives shadowJar
    archives devJar
}
jar.finalizedBy('reobfJar')
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
if (System.getenv('curesforgeapikey') != null) {
    curseforge {
        apiKey = System.getenv('curesforgeapikey')
        project {
            id = "${curesfg_id}"
            changelog = "${curesfg_changelog}"
            releaseType = System.getenv('releasetype')
            addGameVersion "${mc_version}"
            mainArtifact(jar) {
                displayName = "${mod_name}-${mc_version}-${mod_version}"
            }
            addArtifact(devJar) {
                displayName = "${mod_name}-${mc_version}-${mod_version}-dev"
            }
        }
    }
}